cmake_minimum_required(VERSION 4.0.2)

project(programmers-c-coding-test-study C CXX)

set(CMAKE_C_STANDARD 11)
set(CMAKE_CXX_STANDARD 17)

# compile_commands.jsonê°€ í•­ìƒ ìƒì„±ë˜ë„ë¡ ì„¤ì •
set(CMAKE_EXPORT_COMPILE_COMMANDS ON)

# ë¹Œë“œ ì‹œ compile_commands.jsonë¥¼ ë£¨íŠ¸ë¡œ ìë™ ë³µì‚¬
add_custom_target(copy_compile_commands ALL
	COMMAND ${CMAKE_COMMAND} -E copy_if_different
		"${CMAKE_BINARY_DIR}/compile_commands.json"
		"${CMAKE_SOURCE_DIR}/compile_commands.json"
	DEPENDS "${CMAKE_BINARY_DIR}/compile_commands.json"
	COMMENT "Copy compile_commands.json to project root"
	VERBATIM
)

if (WIN32)
    # Windows ì‹œìŠ¤í…œ ë¡œì¼€ì¼ í™•ì¸ì„ ìœ„í•œ í•¨ìˆ˜
    function(get_system_encoding result)
        execute_process(
                COMMAND powershell.exe -NoProfile -Command "[System.Text.Encoding]::Default.WindowsCodePage"
                OUTPUT_VARIABLE codepage
                OUTPUT_STRIP_TRAILING_WHITESPACE
                ERROR_QUIET
        )
        set(${result} ${codepage} PARENT_SCOPE)
    endfunction()

    # ì‹œìŠ¤í…œ ì¸ì½”ë”© í™•ì¸
    get_system_encoding(SYSTEM_CODEPAGE)

    # CP949(MS949), CP65001(UTF-8)ì¸ ê²½ìš°ì— EUC-KR ë˜ëŠ” UTF-8 ì‹¤í–‰ ë¬¸ìì…‹ ì˜µì…˜ ì¶”ê°€
    if (SYSTEM_CODEPAGE EQUAL 949)
        if (MSVC)
            # Visual Studio cl.exeë¥¼ ì‚¬ìš©í•  ë•Œ, ìœˆë„ìš° í„°ë¯¸ë„ í™˜ê²½ì€ CP949 ì¸ë°,
            # ì†ŒìŠ¤íŒŒì¼ì„ UTF-8ë¡œ ì €ì¥í•´ì„œ ê´€ë¦¬í•  ê²½ìš°, ì»´íŒŒì¼ëŸ¬ ì•„ë˜ ì˜µì…˜ì„ ì „ë‹¬í•œë‹¤.
            add_compile_options(/source-charset:utf-8 /execution-charset:euc-kr)
            message(STATUS "Added MSVC charset options for CP949 system encoding")
        elseif (MINGW)
            add_compile_options(-fexec-charset=CP949 -g -W -Wall)
            message(STATUS "Added MinGW charset options for CP949 system encoding")
        endif ()
    elseif (SYSTEM_CODEPAGE EQUAL 65001)
        if (MSVC)
            add_compile_options(/source-charset:utf-8 /execution-charset:utf-8)
            message(STATUS "Added MSVC charset options for UTF-8 system encoding")
        elseif (MINGW)
            add_compile_options(-fexec-charset=UTF-8 -g -W -Wall)
            message(STATUS "Added MinGW charset options for UTF-8 system encoding")
        endif ()
    else ()
        message(STATUS "Using system default encoding (Codepage: ${SYSTEM_CODEPAGE})")
    endif ()
endif ()

# ê³µí†µ í•¨ìˆ˜ ëª¨ìŒ ë¼ì´ë¸ŒëŸ¬ë¦¬
set(COMMON_LIB_PROJECT CommonUtils)
set(COMMON_LIB_NAME ${COMMON_LIB_PROJECT})
add_subdirectory(CommonUtils)

# Google Test ì„¤ì •
include(FetchContent)
FetchContent_Declare(
        googletest
        GIT_REPOSITORY https://github.com/google/googletest.git
        GIT_TAG v1.17.0
)

# Google Testì˜ ì„¤ì •ì´ ìš°ë¦¬ í”„ë¡œì íŠ¸ì— ì˜í–¥ì£¼ì§€ ì•Šë„ë¡
set(gtest_force_shared_crt ON CACHE BOOL "" FORCE)

# ğŸ’¡ gmockì„ ê°™ì´ ë¹Œë“œí•œë‹¤. gmockì—ì„œ ì œê³µë˜ëŠ” matcherê°€ ë‹¤ì–‘í•¨.
set(BUILD_GMOCK ON CACHE BOOL "" FORCE)
set(INSTALL_GTEST OFF CACHE BOOL "" FORCE)

FetchContent_MakeAvailable(googletest)

# GoogleTest ë¹Œë“œ í›„ ê²½ê³  ì–µì œ ì„¤ì •
foreach(tgt gtest gtest_main gmock gmock_main)
    if(TARGET ${tgt})
        target_compile_options(${tgt} PRIVATE -Wno-character-conversion)
    endif()
endforeach()

enable_testing()

message(STATUS "Google Test enabled for testing")

# í…ŒìŠ¤íŠ¸ ì´ë¦„ ìƒì„± í•¨ìˆ˜
function(get_test_target_name LEVEL PROBLEM_ID OUTPUT_VAR)
    set(${OUTPUT_VAR} "lv${LEVEL}_${PROBLEM_ID}_test" PARENT_SCOPE)
endfunction()

# CommonUtils ì—°ê²° í•¨ìˆ˜
function(link_common_utils LEVEL PROBLEM_ID)
    get_test_target_name(${LEVEL} ${PROBLEM_ID} TARGET_NAME)
    target_include_directories(${TARGET_NAME} PRIVATE ../${COMMON_LIB_PROJECT})
    target_link_libraries(${TARGET_NAME} ${COMMON_LIB_NAME})
endfunction()


# í…ŒìŠ¤íŠ¸ íŒŒì¼ ì„¤ì • ì¶”ê°€ë¥¼ ìœ„í•œ ê³µí†µ í•¨ìˆ˜
function(add_coding_test LEVEL PROBLEM_ID EXTENSION)
    get_test_target_name(${LEVEL} ${PROBLEM_ID} TEST_NAME)
    set(TEST_SOURCE_FILE "${PROBLEM_ID}_test.cpp")
    set(TARGET_SOURCE_FILE "${PROBLEM_ID}.${EXTENSION}")

    add_executable(${TEST_NAME}
            ${TEST_SOURCE_FILE}
            ${TARGET_SOURCE_FILE}
    )

    # gtest ë¼ì´ë¸ŒëŸ¬ë¦¬ ë§í¬
    target_link_libraries(${TEST_NAME}
            gtest gtest_main gmock gmock_main
    )

    add_test(NAME ${TEST_NAME} COMMAND ${TEST_NAME})
endfunction()

# ì „ì²´ í…ŒìŠ¤íŠ¸ ì‹¤í–‰ì„ ìœ„í•œ ì»¤ìŠ¤í…€ íƒ€ê²Ÿ
add_custom_target(run_all_tests
        COMMAND ${CMAKE_CTEST_COMMAND} --verbose --output-on-failure
        COMMENT "Running all tests"
        USES_TERMINAL
)

# ê° ë ˆë²¨ë³„ í•˜ìœ„ ë””ë ‰í† ë¦¬ ì¶”ê°€, ë¹Œë“œê°€ í•„ìš”ì—†ì„ ê²½ìš° ì£¼ì„ìœ¼ë¡œ ë‘”ë‹¤.
add_subdirectory(lv00)
add_subdirectory(lv01)
add_subdirectory(lv02)
add_subdirectory(lv03)
